START -> REPTSTART0  .

ADDOP -> plus  . 
ADDOP -> minus  . 
ADDOP -> or  . 

APARAMS -> EXPR REPTAPARAMS1  . 
APARAMS ->  . 

APARAMSTAIL -> comma EXPR  . 

ARITHEXPR -> TERM RIGHTRECARITHEXPR  . 

ARRAYSIZE -> lsqbr Flsqbr .
Flsqbr -> intlit rsqbr .
Flsqbr -> rsqbr .

ASSIGNOP -> equal  . 

ASSIGNSTAT -> VARIABLE ASSIGNOP EXPR  . 

CLASSDECL -> class id OPTCLASSDECL2 lcurbr REPTCLASSDECL4 rcurbr semi  . 

CLASSDECLORFUNCDEF -> CLASSDECL  . 
CLASSDECLORFUNCDEF -> FUNCDEF  . 

EXPR -> FACTOR RIGHTRECTERM RIGHTRECARITHEXPR EXPR2 .
EXPR2 -> RELOP ARITHEXPR  .
EXPR2 -> .

FACTOR -> intlit  . 
FACTOR -> floatlit  . 
FACTOR -> lpar ARITHEXPR rpar  . 
FACTOR -> not FACTOR  . 
FACTOR -> SIGN FACTOR  . 
FACTOR -> id FACTOR2 .
FACTOR2 -> REPTVARIABLE2 FACTOR3 .
FACTOR3 -> dot FACTOR .
FACTOR3 -> .
FACTOR2 -> lpar APARAMS rpar FACTOR4 .
FACTOR4 -> dot FACTOR .
FACTOR4 -> .

FPARAMS -> id colon TYPE REPTFPARAMS3 REPTFPARAMS4  . 
FPARAMS ->  . 

FPARAMSTAIL -> comma id colon TYPE REPTFPARAMSTAIL4  . 

FUNCBODY -> lcurbr REPTFUNCBODY1 rcurbr  . 

FUNCDEF -> FUNCHEAD FUNCBODY  . 

FUNCHEAD -> function Ffunction .
Ffunction -> id Fid2 .
Fid2 -> lpar FPARAMS rpar arrow RETURNTYPE .
Fid2 -> sr Fsr .
Fsr -> id lpar FPARAMS rpar arrow RETURNTYPE .
Fsr -> constructor lpar FPARAMS rpar .

FUNCTIONCALL -> id FFUNCid .
FFUNCid -> lpar APARAMS rpar FFUNCid2 .
FFUNCid -> REPTIDNEST1 dot FUNCTIONCALL.
FFUNCid2 -> dot FUNCTIONCALL.
FFUNCid2 -> .

IDNEST -> id Fid3 .
Fid3 -> REPTIDNEST1 dot .
Fid3 -> lpar APARAMS rpar dot .

INDICE -> lsqbr ARITHEXPR rsqbr  . 

LOCALVARDECL -> localvar Flocalvar .
Flocalvar -> id Fid .
Fid -> colon Fcolon .
Fcolon -> TYPE FTYPE .
FTYPE -> REPTLOCALVARDECL4 semi .
FTYPE -> lpar APARAMS rpar semi .

LOCALVARDECLORSTMT -> LOCALVARDECL  . 
LOCALVARDECLORSTMT -> STATEMENT  . 

MEMBERDECL -> MEMBERFUNCDECL  . 
MEMBERDECL -> MEMBERVARDECL  . 

MEMBERFUNCDECL -> function id colon lpar FPARAMS rpar arrow RETURNTYPE semi  . 
MEMBERFUNCDECL -> constructor colon lpar FPARAMS rpar semi  . 

MEMBERVARDECL -> attribute id colon TYPE REPTMEMBERVARDECL4 semi  . 

MULTOP -> mult  . 
MULTOP -> div  . 
MULTOP -> and  . 

OPTCLASSDECL2 -> isa id REPTOPTCLASSDECL22  . 
OPTCLASSDECL2 ->  . 

RELEXPR -> ARITHEXPR RELOP ARITHEXPR  . 

RELOP -> eq  . 
RELOP -> neq  . 
RELOP -> lt  . 
RELOP -> gt  . 
RELOP -> leq  . 
RELOP -> geq  . 

REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1  . 
REPTAPARAMS1 ->  . 

REPTCLASSDECL4 -> VISIBILITY MEMBERDECL REPTCLASSDECL4  . 
REPTCLASSDECL4 ->  . 

REPTFPARAMS3 -> ARRAYSIZE REPTFPARAMS3  . 
REPTFPARAMS3 ->  . 

REPTFPARAMS4 -> FPARAMSTAIL REPTFPARAMS4  . 
REPTFPARAMS4 ->  . 

REPTFPARAMSTAIL4 -> ARRAYSIZE REPTFPARAMSTAIL4  . 
REPTFPARAMSTAIL4 ->  . 

REPTFUNCBODY1 -> LOCALVARDECLORSTMT REPTFUNCBODY1  . 
REPTFUNCBODY1 ->  . 

REPTFUNCTIONCALL0 -> IDNEST REPTFUNCTIONCALL0  . 
REPTFUNCTIONCALL0 ->  . 

REPTIDNEST1 -> INDICE REPTIDNEST1  . 
REPTIDNEST1 ->  . 

REPTLOCALVARDECL4 -> ARRAYSIZE REPTLOCALVARDECL4  . 
REPTLOCALVARDECL4 ->  . 

REPTMEMBERVARDECL4 -> ARRAYSIZE REPTMEMBERVARDECL4  . 
REPTMEMBERVARDECL4 ->  . 

REPTOPTCLASSDECL22 -> comma id REPTOPTCLASSDECL22  . 
REPTOPTCLASSDECL22 ->  . 

REPTSTART0 -> CLASSDECLORFUNCDEF REPTSTART0  . 
REPTSTART0 ->  . 

REPTSTATBLOCK1 -> STATEMENT REPTSTATBLOCK1  . 
REPTSTATBLOCK1 ->  . 

REPTVARIABLE2 -> INDICE REPTVARIABLE2  . 
REPTVARIABLE2 ->  . 

RETURNTYPE -> TYPE  . 
RETURNTYPE -> void  . 

RIGHTRECARITHEXPR ->  . 
RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR  . 

RIGHTRECTERM ->  . 
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM  . 

SIGN -> plus  . 
SIGN -> minus  .  

STATBLOCK -> lcurbr REPTSTATBLOCK1 rcurbr  . 
STATBLOCK -> STATEMENT  . 
STATBLOCK ->  . 
 
STATEMENT -> if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi  . 
STATEMENT -> while lpar RELEXPR rpar STATBLOCK semi  . 
STATEMENT -> read lpar VARIABLE rpar semi  . 
STATEMENT -> write lpar EXPR rpar semi  . 
STATEMENT -> return lpar EXPR rpar semi  . 
STATEMENT -> id STATEMENT2 .
STATEMENT2 -> REPTVARIABLE2 STATEMENT3 .
STATEMENT3 -> dot STATEMENT .
STATEMENT3 -> semi .
STATEMENT3 -> ASSIGNOP EXPR semi .
STATEMENT2 -> lpar APARAMS rpar STATEMENT4 .
STATEMENT4 -> dot STATEMENT .
STATEMENT4 -> semi .
STATEMENT4 -> ASSIGNOP EXPR semi . 

TERM -> FACTOR RIGHTRECTERM  . 

TYPE -> integer  . 
TYPE -> float  . 
TYPE -> id  . 

VARIABLE -> id FVARid .
FVARid -> REPTVARIABLE2 FVARid2. 
FVARid -> lpar APARAMS rpar dot VARIABLE .
FVARid2 -> dot VARIABLE .
FVARid2 -> .

VISIBILITY -> public  . 
VISIBILITY -> private  . 
VISIBILITY ->  . 
